#Q11: Trips of a particular route that are available on a certain date  
#original.q11.ROUTEID=4__6___
#original.q11.DATE="2017-07-02"

#vig.q11.ROUTEID=0000000000000000000a
#vig.q11.DATE="2017-07-02"


PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX gtfs: <http://vocab.gtfs.org/terms#>
PREFIX geo: <http://www.w3.org/2003/01/geo/wgs84_pos#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX schema: <http://schema.org/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX gtfsroute: <http://transport.linkeddata.es/madrid/metro/routes/>

SELECT * WHERE {
        ?service a gtfs:Service .
        ?service gtfs:serviceRule ?calendarRule .
		
		?trip gtfs:service ?service .
		?trip gtfs:route gtfsroute:0000000000000000000a  .
		
		?calendarRule a gtfs:CalendarRule .
       # ?calendarRule gtfs:sunday "true"^^xsd:boolean . 
        ?calendarRule schema:startDate ?startDate .
        ?calendarRule schema:endDate ?endDate .
        FILTER (?startDate < "2017-07-02"^^xsd:date) .
        FILTER (?endDate > "2017-07-02"^^xsd:date) .
		
        FILTER NOT EXISTS {
		    ?service gtfs:serviceRule ?calendarDateRule . 
            ?calendarDateRule a gtfs:CalendarDateRule .
	        ?calendarDateRule dct:date "2017-07-02"^^xsd:date .
	        ?calendarDateRule gtfs:dateAddition "false"^^xsd:boolean 
        }
}