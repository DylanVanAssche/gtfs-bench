#Q3A: List all wheelchair accessible stops along a particular route. Datasets: Routes, Trips, StopTimes, Stops

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX gtfs: <http://vocab.gtfs.org/terms#>
PREFIX geo: <http://www.w3.org/2003/01/geo/wgs84_pos#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX gtfsroute: <http://linkedgtfs.com/CITY/routes/>

SELECT * WHERE {
	gtfsroute:4__1___ a gtfs:Route .
	OPTIONAL { gtfsroute:4__1___ gtfs:shortName ?routeShortName . }
	OPTIONAL { gtfsroute:4__1___ dct:description ?routeDescription . }

	?trip a gtfs:Trip .
	OPTIONAL { ?trip gtfs:shortName ?tripShortName . }
	?trip gtfs:service ?service .
	?trip gtfs:route gtfsroute:4__1___ .

	?stopTime a gtfs:StopTime . 
	?stopTime gtfs:trip ?trip . 
	?stopTime gtfs:stop ?stop . 

	?stop a gtfs:Stop . 
	OPTIONAL { ?stop dct:description ?stopDescription . }
	OPTIONAL {
		?stop geo:lat ?stopLat . 
		?stop geo:long ?stopLong . 
	}
	?stop gtfs:wheelchairBoarding ?stopWheelchairBoarding .
	FILTER (?stopWheelchairBoarding =  "true"^^xsd:boolean) .
}
